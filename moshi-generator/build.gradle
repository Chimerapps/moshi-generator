buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'com.chimerapps'
version '0.1.0'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

dependencies {
    compile 'com.squareup:javapoet:1.9.0'
    compile 'com.squareup.moshi:moshi:1.8.0'
    compile "com.chimerapps:moshi-generator-runtime:$ARTIFACT_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    compileOnly project(":moshi-generator-runtime")
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Nicola Verbeeck"
            email "nicola@chimerapps.com"
            organization "Chimerapps"
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'moshi-generator'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Chimerapps/moshi-generator'

        version {
            name = "$ARTIFACT_VERSION"
            desc = "Moshi adapter generator"
            released = new Date()
        }
    }
    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId "$ARTIFACT_PACKAGE"
            version "$ARTIFACT_VERSION"
            artifactId "$ARTIFACT_NAME"
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact packageJavadoc {
                classifier "javadoc"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Annotation processor that generates moshi adapters')
                root.appendNode('name', 'moshi-generator')
                root.appendNode('url', 'https://github.com/Chimerapps/moshi-generator')
                root.children().last() + pomConfig
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }
}
